import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.4.RELEASE")
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'

group = 'org.wickedsource'
version = '1.0.0.SNAPSHOT'

ext{
    snippetsDir = file('build/generated-snippets')
}

test{
    outputs.dir snippetsDir
}

asciidoctor {
    attributes  'snippets': snippetsDir,
                'version': version,
                'date': new SimpleDateFormat("yyyy-MM-dd").format(new Date())
    inputs.dir snippetsDir
    dependsOn test
    sourceDir 'src/main/asciidoc'
    outputDir 'doc/restapi'
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.3.3.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-hateoas:1.3.3.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.3.3.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.1.0.201509280440-r'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'io.fabric8', name: 'gitective-core', version: '0.9.10'
    compile 'com.h2database:h2:1.4.191'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.hibernate:hibernate-validator:5.2.4.Final'

    compile project(':plugins:loc-analyzer-plugin')
    compile project(":plugins:analyzer-plugin-api")

    runtime group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.3.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.21'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.0.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-core:1.1.0.RELEASE'
}

bootRun {
    jvmArgs =
            [
                    "-Xdebug",
                    "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005",
                    "-Dlogging.level.org.wickedsource.coderadar=DEBUG",
                    "-Dlogging.level.org.wickedsource.coderadar.job.JobLogger=INFO",
                    "-Dlogging.level.org.hibernate.SQL=ERROR",
                    "-Dlogging.level.org.hibernate.type=ERROR",
                    "-Dlogging.file=coderadar.log",

                    "-Dcoderadar.master=false",
                    "-Dcoderadar.slave=false",
                    "-Dcoderadar.scanIntervalInSeconds=30",
                    "-Dcoderadar.workdir=C:/coderadar",

                    "-Dspring.jpa.hibernate.ddl-auto=create-drop",

                    // use these for file based H2
//                    "-Dspring.datasource.url=jdbc:h2:file:C:/coderadar/database",
//                    "-Dspring.datasource.username=sa",
//                    "-Dspring.datasource.password=sa",
//                    "-Dspring.datasource.driverClassName=org.h2.Driver",

                    // use these for My SQL
                    "-Dspring.datasource.url=jdbc:mysql://localhost/coderadar",
                    "-Dspring.datasource.username=coderadar",
                    "-Dspring.datasource.password=coderadar",
                    "-Dspring.datasource.driver-class-name=com.mysql.jdbc.Driver",
            ]
}