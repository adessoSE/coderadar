buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "org.sonarqube" version "2.8"
}

apply plugin: 'idea'

ext {
    majorVersion = '0'
    minorVersion = '5'
    patchVersion = '0'
    baseVersion = "${majorVersion}.${minorVersion}.${patchVersion}"
}

// import all scripts from ./gradle.d in order of sequence
file('gradle.d').listFiles().sort().each {
    if (it =~ /.*\.gradle$/) {
        apply from: relativePath(it)
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "adessoAG_coderadar"
        property "sonar.organization", "adesso-ag"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: "idea"
    apply plugin: "java"

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }

        dependencies {
            classpath "org.springframework.boot:spring-boot-gradle-plugin:${version_spring_boot}"
            classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
            classpath "org.ajoberstar:gradle-git-publish:2.1.2"
            classpath "com.diffplug.spotless:spotless-plugin-gradle:3.26.0"
            classpath "com.github.jacobono:gradle-jaxb-plugin:1.3.6"
            classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        }
    }

    version = project.getBuildVersion(baseVersion)

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
        testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: version_mockito_junit
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: version_junit
        annotationProcessor("org.projectlombok:lombok:$lombok_version")
        compileOnly("org.projectlombok:lombok:$lombok_version")
    }

}

idea {
    project {
        vcs = 'Git'
    }
}
